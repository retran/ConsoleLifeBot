plugins {
    id "net.ltgt.apt" version "0.9"
    id 'org.hidetake.ssh' version "2.7.0"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

group 'me.retran.consolelifebot'
version '0.1.0'

sourceCompatibility = 1.8
mainClassName = "me.retran.consolelifebot.Application"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

repositories {
    mavenCentral()
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes 'Main-Class': 'me.retran.consolelifebot.Application'
    }
}

task copyJar(type: Copy) {
    from 'build/libs'
    from 'Dockerfile'
    into 'package'
}

copyJar.dependsOn jar

task tgz(type: Tar) {
    from './package/'
    baseName = 'package'
    destinationDir = file('.')
    extension = 'tgz'
    compression = Compression.GZIP
}

tgz.dependsOn copyJar

remotes {
    bots {
        host = '128.199.61.99'
        user = 'root'
        identity = file('/home/retran/.ssh/id_rsa')
    }
}

task deploy << {
    ssh.run {
        session(remotes.bots) {
            put from: "package-0.1.0.tgz", into: "/app"
            execute 'tar -xvf /app/package-0.1.0.tgz -C /app'
            execute 'docker build -t retran/consolelifebot:v0.1.0 /app/'
            execute 'docker kill clb'
            execute 'docker rm clb'
            execute 'docker run -d --name clb retran/consolelifebot:v0.1.0'
            execute 'docker images --no-trunc -aqf "dangling=true" | xargs docker rmi'
        }
    }
}

deploy.dependsOn tgz

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.8'
    compile 'com.google.dagger:dagger:2.8'
    compile group: 'org.telegram', name: 'telegrambots', version: '2.4.4'
    compile 'com.google.api-client:google-api-client:1.22.0'
    compile 'com.google.apis:google-api-services-youtube:v3-rev180-1.22.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

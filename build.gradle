plugins {
    id "net.ltgt.apt" version "0.9"
    id 'org.hidetake.ssh' version "2.7.0"
    id 'java'
    id 'application'
    id 'eclipse'
}

group 'me.retran.consolelifebot'
version '0.1.0'

sourceCompatibility = 1.8
mainClassName = "me.retran.consolelifebot.Application"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

repositories {
    mavenCentral()
}

distributions {
    main {
       contents {
           from('Dockerfile') {
               into '.'
           }
       }
    }
}

remotes {
    bots {
        host = '128.199.61.99'
        user = 'root'
        identity = file('/home/retran/.ssh/id_rsa')
        agent = true
    }
}

task deploy << {
    ssh.run {
        session(remotes.bots) {
            execute 'rm -rf /app', ignoreError: true
            execute 'mkdir /app'
            put from: "./build/distributions/consolelifebot-0.1.0.tar", into: "/app"
            execute 'tar -xvf /app/consolelifebot-0.1.0.tar -C /app'
            execute 'docker build -t retran/consolelifebot:v0.1.0 /app/consolelifebot-0.1.0'
            execute 'docker kill clb', ignoreError: true
            execute 'docker rm clb', ignoreError: true
            execute 'docker run -d --name clb retran/consolelifebot:v0.1.0'
            execute 'docker images --no-trunc -aqf "dangling=true" | xargs docker rmi', ignoreError: true
        }
    }
}

deploy.dependsOn distTar 

dependencies {
    apt 'com.google.dagger:dagger-compiler:2.8'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.google.dagger:dagger:2.8'
    compile group: 'org.telegram', name: 'telegrambots', version: '2.4.4'
    compile 'com.google.api-client:google-api-client:1.22.0'
    compile group: 'com.instagram', name: 'ig-json-parser-processor', version: '0.0.6+'
    compile 'com.google.apis:google-api-services-youtube:v3-rev180-1.22.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
